#!/usr/bin/env coffee

config = require '../lib/config/config'
config.load()

showError = (err) ->
  console.log "ERROR: #{err}"

nomnom = require 'nomnom'
nomnom.script('mailr')
nomnom.command('send')
  .options
    account:
      abbr: 'a'
      metavar: 'ACCOUNT'
      help: 'the account from which to send'
    emailFile:
      position: 1
      help: 'name of email file to send'
      required: true
  .help("send an email")
  .callback (opts) ->
    sender = require '../lib/sender'
    console.log "Sending..."
    sender.send(filename: opts.emailFile, account: opts.account) \
      .then (response) ->
        console.log response
      .fail (err) ->
        console.log "ERROR: #{err}"

nomnom.command('contacts')
  .options
    account:
      abbr: 'a'
      metavar: 'ACCOUNT'
      required: true
      help: 'the account from which to send'
    update:
      abbr: 'u'
      flag: true
      help: 'updates the local contacts file'
  .help("gets the contacts")
  .callback (opts) ->
    contacts = require '../lib/contacts'
    promise = null
    console.log opts.account
    if opts.update
      promise = contacts.updateContacts(opts.account)
    else
      promise = contacts.getContacts(opts.account)

    showContacts = (contacts) ->
      for contact in contacts
        console.log contact

    promise.then(showContacts, showError)

nomnom.command('addresses')
  .help("shows the available from addresses")
  .callback ->
    for address in config.getFromAddresses()
      console.log address

nomnom.command('mailboxes')
  .help("shows the mailboxes")
  .options
    account:
      abbr: 'a'
      metavar: 'ACCOUNT'
      required: true
      help: 'the account from which to send'
  .callback (opts) ->
    ImapClient = require '../lib/clients/imap_client'
    config.accounts[opts.account].getImapSettings().then (settings) ->
      client = new ImapClient()
      client.connect(settings).then ->
        client.getMailboxes().then (mailboxes) ->
          console.log mailboxes

nomnom.parse()
